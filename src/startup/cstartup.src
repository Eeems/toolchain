;-------------------------------------------------------------------------------
; Standard CE startup module definitions and references
;-------------------------------------------------------------------------------
	assume	adl=1

        public _errno
_errno := 0D008DCh

;-------------------------------------------------------------------------------
	section	.header
	db	0EFh
	db	07Bh
	db	0		; Magic byte recognition for C programs
        public __init
__init:

;-------------------------------------------------------------------------------
	section	.startup

	public  __startup
__startup:
	call	0020848h	; _RunInicOff, assumes iy = flags
	call	00004F4h	; _usb_DisableTimer
	di			; disable interrupts
	call	_os_DisableHomeTextBuffer
	ld	hl,0E00305h
	push	hl		; save flash wait state control port
	ld	a,(hl)
	push	af		; save flash wait states
	ld	(hl),h		; set flash wait states to 3; 1 was definitely
				; too low for some calcs, 2 is untested
	push	iy		; save iy (flags)
	ld	(__exitsp+1),sp	; save sp for exiting
	ld	bc,__len_bss
	ld	hl,__low_bss
	push	bc
	push	hl
	call	0000B0h		; __memclear, clear BSS
				; args will get removed from stack on exit
	call	_main
        public	__exit
__exit:
        public	__exitsp
__exitsp:
        public	__errsp
__errsp:
	ld	sp,0
	pop	iy		; iy = flags
	pop	af		; a = original flash wait states
	ex	(sp),hl		; hl = flash wait state control port,
				; save exit code
	ld	(hl),a		; restore flash wait states
	call	00004F0h	; _usb_ResetTimer
	call	_os_EnableHomeTextBuffer

	extern	_main
	extern	__low_bss
	extern	__len_bss
	extern	_os_DisableHomeTextBuffer
	extern	_os_EnableHomeTextBuffer

;-------------------------------------------------------------------------------
	section	.exit
	pop	hl		; hl = exit code
	ret

        public	_exit
_exit:
	pop	hl
	pop	hl
	jr	__exit

;-------------------------------------------------------------------------------
